plugins {
    id 'de.undercouch.download' version '5.3.0'
    id 'de.dfki.mary.voicebuilding-legacy'
}

group 'de.dfki.mary'
version '0.1.0-SNAPSHOT'

dependencies {
    marytts group: 'de.dfki.mary', name: 'marytts-lang-hsb', version: '0.1.0'
}

configurations.all {
    resolutionStrategy {
        force 'de.dfki.mary:marytts-lang-hsb:0.1.0'
    }
}

marytts {
    voice {
        name = 'serbski-institut-hsb-matej'
        gender = 'male'
        language = 'hsb'
        type = 'unit selection'
        samplingRate = 16000
    }
}

tasks.register('downloadData') {
    def flacFile = objects.fileProperty()
            .convention(layout.buildDirectory.file('serbski-institut-hsb-data.flac'))
    def yamlFile = objects.fileProperty()
            .convention(layout.buildDirectory.file('serbski-institut-hsb-data.yaml'))
    outputs.file flacFile
    outputs.file yamlFile
    doLast {
        def baseUrl = 'https://github.com/marytts/serbski-institut-hsb-data/releases/download/v0.1.0'
        download.run {
            src([
                    "$baseUrl/serbski-institut-hsb-data.flac",
                    "$baseUrl/serbski-institut-hsb-data.yaml"
            ])
            dest buildDir
            overwrite false
        }
    }
}

wav {
    srcDir = layout.projectDirectory.dir('wav')
}

basenames {
    textDir = layout.projectDirectory.dir('text')
    labDir = layout.projectDirectory.dir('lab')
}

generateAllophones {
    srcDir = layout.projectDirectory.dir('text')
}

processPhoneLabels {
    srcDir = layout.projectDirectory.dir('lab')
}
